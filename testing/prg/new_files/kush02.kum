| - - - - - Содержимое файла "2.kum"   
использовать Файлы

алг Тест_2 
нач файл Ключ, цел ответ_м1, ответ_м2, вещ а 
лит имяФайла
имяФайла := "/tmp/02.txt"

Ключ:=открыть на запись(имяФайла)
вывод Ключ, "-1 2 8 -3 4 -5 6 -7    8 2 -9" 
закрыть(Ключ)
Ключ:=открыть на чтение(имяФайла )
НайтиНомераМаксимальных(Ключ,ответ_м1, ответ_м2) 
вывод "Номера первого и последнего максимальных элементов последовательности", нс 
начать чтение(Ключ) 
нц пока не конец файла(Ключ) 
ввод Ключ, а 
вывод а, " " 
кц 
вывод нс, "равны ", ответ_м1, " и ", ответ_м2, " соответственно", нс 
закрыть(Ключ) 
утв ответ_м1=3 и ответ_м2=9 
вывод "ОК" 
кон 

алг НайтиНомераМаксимальных(арг файл Ключ, рез цел ответ_м1, ответ_м2 ) 
дано | Последовательность вещественных чисел   
|        в открытом файле с кодом Ключ   
надо  |  Ответ_м1 = номер первого максимального числа последовательности;   
|          Ответ_м2 = номер последнего максимального числа последовательности;   
|          для пустой последовательности алгоритм должен завершаться аварийно;   
нач вещ а, вещ М, цел число_элементов, м1, м2 
| M – максимальное среди прочитанных чисел последовательности,   
| м1 - номер первого максимального среди прочитанных,   
| м2 – номер последнего максимального среди прочитанных.   
начать чтение(Ключ) 
если конец файла(Ключ) то утв  0=1 
иначе 
ввод Ключ,а 
М:=а; число_элементов:=1; м1:=1; м2:=1 
нц пока не конец файла(Ключ) 
ввод Ключ,а 
число_элементов:=число_элементов +1 
выбор 
при а>М: м1:=число_элементов; м2:=число_элементов; М:=а 
при а=М: м2:=число_элементов 
все 
кц 
все 
ответ_м1:=м1 
ответ_м2:=м2 

кон 
| - - - - - Конец содержимого файла "2.kum"   








