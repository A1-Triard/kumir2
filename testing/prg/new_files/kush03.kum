| - - - - - Содержимое файла "3.kum"   

использовать Файлы

алг Тест_3 
нач цел  ответ, вещ а 
лит имяФайла
файл Ключ
имяФайла := "/tmp/03.txt"
Ключ:=открыть на запись(имяФайла)
вывод Ключ, "0 -1 2 -3 4 -9 -5 6 -7 8 -9 10"
закрыть(Ключ)
Ключ:=открыть на чтение(имяФайла)
НайтиЧислоМинимальных(Ключ,ответ) 
вывод "Число Минимальных элементов последовательности", нс 
начать чтение(Ключ) 
нц пока не конец файла(Ключ) 
ввод Ключ,а
вывод а, " " 
кц 
вывод нс, "равно ", ответ, нс 
закрыть(Ключ) 
утв ответ=2 
вывод "ОК" 
кон 

алг НайтиЧислоМинимальных(арг файл Ключ, рез цел ответ) 
дано | Последовательность вещественных чисел   
|        в открытом файле с кодом Ключ   
надо  | Ответ = число минимальных элементов последовательности;   
|         для пустой последовательности алгоритм должен завершаться аварийно;   
нач вещ а, М, цел м 
| М – минимальный среди прочитанных элементов последовательности;   
| м – число минимальных элементов среди прочитанных   
начать чтение(Ключ) 
если конец файла(Ключ) то утв  0=1 
иначе 
ввод Ключ,а
м:=1; М:=а; 
нц пока не конец файла(Ключ) 
ввод Ключ,а
выбор 
при а > М: | ничего не делать   
при а = М: м:=м+1 
при а < М: м:=1 
все 
М:=min(М,а) 
кц 
ответ:=м 
все 
кон 
| - - - - - Конец содержимого файла "3.kum"   







